plugins {
  id "com.diffplug.gradle.spotless" version "3.28.1"
  id "org.springframework.boot" version "2.2.6.RELEASE"
  id "io.spring.dependency-management" version "1.0.9.RELEASE"
  id "io.freefair.lombok" version "5.0.0"
  id "java"
}

group = 'net.iceburg'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

processResources {
  filesMatching('application.yml') {
    expand(project.properties)
  }
}

springBoot {
  buildInfo()
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  implementation 'net.iceburg:iceburg-spring-boot-starter'
  implementation 'net.iceburg:skytale-spring-boot-starter'

  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
}

spotless {
  java {
    target 'src/main/java/**/*.java', 'src/test/java/**/*.java'
    //googleJavaFormat()
  }
}

task('mkDocker', type: Exec) {
  group = 'Docker'
  description "Assembles JAR and builds docker image"
  dependsOn assemble

  def imageTag = System.getenv("PIPELINE_ID") ?: "local-0"
  commandLine 'docker', 'build',
          '-t', "${project.name}:${imageTag}",
          '-t', "${project.name}:${version}",
          '.'
}

test {
  useJUnitPlatform()
}
